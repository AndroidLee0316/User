apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'uploadArchives.gradle'


android {
    compileSdkVersion rootProject.ext.compile_sdk_version

    defaultConfig {
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode rootProject.ext.version_code
        versionName rootProject.ext.version_name
        buildConfigField "String", "LOG_URL", '"https://biap-adm.pingan.com.cn/"'

        /**
         * ARouter 路由配置
         */
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
            }
        }

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    //娜迦加固后的so必须配置该选项，不然会出现 dynamic section has invalid link(0) sh_type: 0 (expected SHT_STRTAB)
    packagingOptions {
        doNotStrip "*/arm64-v8a/*.so"
        doNotStrip "*/armeabi-v7a/*.so"
        doNotStrip "*/armeabi/*.so"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = ["act_export": false]
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = ["act_export": true]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            res.srcDirs += "src/main/res-theme"
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    api "com.android.support:appcompat-v7:${rootProject.ext.support_lib_version}"
    if (isMavenBuild == 'false') {
        api project(':LibUserBase')
        api project(':LibPALiveDetect')
    } else {
        api "com.zxs.lib:userbase:1.0.0"
        api "com.zxs.lib:PALiveDetect:1.0.0"
    }

    kapt "com.alibaba:arouter-compiler:${rootProject.ext.arouterCompiler}"
    api 'com.github.AndroidLee0316:router:support_1.0.0'
}
